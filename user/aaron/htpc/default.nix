{ pkgs, ... }:
let
  latestSourceURL = name: "https://addons.mozilla.org/firefox/downloads/latest/${name}/latest.xpi";
in
{
  home-manager.users.aaron = {
    home.pointerCursor = {
      gtk.enable = true;
      package = pkgs.bibata-cursors;
      name = "Bibata-Modern-Ice";
      size = 96;
    };

    stylix.targets.librewolf.profileNames = [ "yt-kiosk" ];
    stylix.targets.swaylock.useWallpaper = false;

    xdg.desktopEntries = {
      youtube = {
        name = "YouTube";
        genericName = "LibreWolf YouTube Kiosk";
        exec = "librewolf --profile /home/aaron/.librewolf/yt-kiosk --kiosk https://www.youtube.com %U";
        terminal = false;
        icon = ../../../images/icons/arrow.svg;
        categories = [
          "Application"
          "WebBrowser"
          "Video"
        ];
      };
      gamescope = {
        name = "Gamescope";
        genericName = "Steam Gamescope Session";
        exec = "gamescope-session %U";
        terminal = false;
        icon = ../../../images/icons/steam.png;
        categories = [
          "Application"
          "Game"
        ];
      };
    };

    programs = {
      wofi = {
        enable = true;
        settings = {
          allow_images = true;
          image_size = 128;
          no_actions = true;
          hide_search = true;
          hide_scroll = true;
          columns = 1;
        };
      };
      librewolf = {
        enable = true;
        policies = {
          Cookies.Allow = [ "https://www.youtube.com" ];
          DisplayBookmarksToolbar = "never";

          ExtensionSettings = {
            "sponsorBlocker@ajay.app" = {
              default_area = "menupanel";
              install_url = latestSourceURL "sponsorblock";
              installation_mode = "force_installed";
              locked = true;
            };
            "cb-remover@search.mozilla.org" = {
              default_area = "menupanel";
              install_url = latestSourceURL "clickbait-remover-for-youtube";
              installation_mode = "force_installed";
              locked = true;
            };
            "uBlock0@raymondhill.net" = {
              default_area = "menupanel";
              install_url = latestSourceURL "ublock-origin";
              installation_mode = "force_installed";
              private_browsing = true;
              locked = true;
            };
          };

          Preferences = {
            "browser.tabs.inTitlebar" = false;
            "kiosk.mode" = true;
            "kiosk.url" = "https://www.youtube.com";
          };
        };
      };

      swaylock = {
        enable = true;
        settings = {
          image = ../../../images/lockscreen/bus.jpg;
          indicator-radius = 100;
          indicator-thickness = 20;
          font-size = 96;
          show-failed-attempts = true;
        };
      };

      fuzzel.enable = true;

      kodi = {
        enable = true;
        package = pkgs.kodi-wayland.withPackages (exts: [
          exts.jellyfin
          exts.sponsorblock
          exts.libretro
          exts.upnext
          exts.inputstream-adaptive
        ]);
        addonSettings = {
          "plugin.video.invidious" = {
            auto_instance = "false";
            instance_url = "http://x86-atxtwr-computeserver:3004";
          };
        };
      };
    };

    services = {
      gammastep.enable = true;

      swayidle = {
        enable = true;
        extraArgs = [ "-w" ];
        events = [
          {
            event = "before-sleep";
            command = "${pkgs.swaylock}/bin/swaylock -fF";
          }
        ];
        timeouts = [
          {
            timeout = 600; # 10min
            command = "${pkgs.swaylock}/bin/swaylock -f -c 000000";
          }
          {
            timeout = 1200; # 20min
            command = "${pkgs.systemd}/bin/systemctl sleep";
          }
        ];
      };
    };

    wayland.windowManager.hyprland = {
      enable = true;
      systemd.enable = false;
      settings = {
        "$terminal" = "wezterm";
        "$menu" = "wofi --show drun";
        "$mainMod" = "CTRL";

        autogenerated = 0;

        bind = [
          "$mainMod, Q, exec, $terminal"
          "$mainMod, C, killactive"
          "$mainMod, M, exit"
          "$mainMod, F, fullscreen, 0"
          "$mainMod, R, exec, $menu"
          "$mainMod SHIFT, 1, movetoworkspace, 1"
          "$mainMod SHIFT, 2, movetoworkspace, 2"
          "$mainMod SHIFT, 3, movetoworkspace, 3"
          "$mainMod SHIFT, 4, movetoworkspace, 4"
          "$mainMod SHIFT, 5, movetoworkspace, 5"
          "$mainMod CTRL SHIFT, h, movetoworkspace, m-1"
        ];

        env = [
          "XCURSOR_SIZE,96"
          "HYPRCURSOR_SIZE,96"
          "XDG_SESSION_TYPE,wayland"
        ];

        windowrule = [
          "fullscreen, class:*"
          "workspace 1, class:^(kodi)(.*)$"
          "workspace 2, class:^(steam)(.*)$"
          "workspace 3, class:^(librewolf)(.*)$"
        ];
      };
    };
  };
}
